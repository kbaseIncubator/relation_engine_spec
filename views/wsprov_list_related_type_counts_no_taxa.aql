// Fetch the counts of a ws_objects in the RE that is linked to a wsprov_object by an object other than a taxon
// Args:
//   ws_ids - array of private workspace ids the user has access to (autofilled by RE-API)
//   key - key of the object to start from
//   show_private - if present, limit to objects in workspaces that a user has access to
//   show_public - if present, limit to objects in public workspaces
//   *** if both show_private and show_private are true this will be treated as an OR ***
//   owners - if truthy, limit to objects with owner in list
//   simplify_type - if true, strip out the module and version before collecting
//   max_depth - longest path to explore

WITH wsprov_object
FOR v, e, p IN 1..@max_depth
    ANY CONCAT('wsprov_object/', @key) wsprov_links
    OPTIONS {'uniqueVertices': 'global', 'bfs':true}
    // No links thru Taxa
    FILTER p.vertices[*].ws_type != "KBaseGenomeAnnotations.Taxon-1.0"
    // If both show_private and show_public, return if either is true
    FILTER (@show_private && @show_public) ? (v.is_public || v.workspace_id IN @ws_ids) :
        (!@show_private || v.workspace_id IN @ws_ids) && (!@show_public || v.is_public)
    FILTER (!@owners || v.owner IN @owners)
    LET ws_type = @simplify_type ? SPLIT(SPLIT(v.ws_type, '-', 1)[0], '.')[1] : v.ws_type
    COLLECT type = ws_type WITH COUNT INTO type_count
    RETURN {type, type_count}
